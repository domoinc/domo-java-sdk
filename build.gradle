plugins {
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '2.3.0'
    id 'ru.vyarus.github-info' version '1.3.0'
    id 'ru.vyarus.animalsniffer' version '1.5.4'
    id 'ru.vyarus.quality' version '4.7.0'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'net.researchgate.release' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

description = 'Official Domo Java SDK'

github {
    user = 'domoinc'
    license = 'MIT'
}

allprojects {
    if(project.hasProperty("buildNumber")) {
        project.version = project.version + "-" + project.buildNumber
    }
    group = 'com.domo'

    repositories { jcenter(); mavenCentral(); mavenLocal() }
    apply plugin: 'java'
    apply plugin: "ru.vyarus.java-lib"
    apply plugin: 'idea'

    pom {
        developers {
            developer {
                id 'domoinc'
                name 'Domo'
                email 'clint.checketts@domo.com'
            }
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'domo-java-sdk'
        userOrg = 'domoinc'
        name = project.name
        desc = project.description
        labels = []
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = false
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}


dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}
